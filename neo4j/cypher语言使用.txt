
更详细的neo4j查询语句可见： 
https://neo4j.com/docs/developer-manual/3.2/cypher/syntax/
http://neo4j.com/docs/2.2.0/query-functions-scalar.html
https://www.w3cschool.cn/neo4j/neo4j_cql_merge.html

创建测试数据：
浏览器打开：http://localhost:7474/browser/
输入：`:play movie graph`
或`:play northwind-graph`即可获取数据；

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/products.csv" AS row
CREATE (n:Product)
SET n = row,
  n.unitPrice = toFloat(row.unitPrice),
  n.unitsInStock = toInteger(row.unitsInStock), n.unitsOnOrder = toInteger(row.unitsOnOrder),
  n.reorderLevel = toInteger(row.reorderLevel), n.discontinued = (row.discontinued <> "0")
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/categories.csv" AS row
CREATE (n:Category)
SET n = row
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/suppliers.csv" AS row
CREATE (n:Supplier)
SET n = row
Create indexes


 neo4j数据库简单除暴理解的概念：

Neo4j中不存在表的概念，只有两类：节点(Node)和关联(Relation)，可以简单理解为图里面的点和边。
在数据查询中，节点一般用小括号()，关联用中括号[]。
当然也隐含路径的概念，是用节点和关联表示的，如：(a)-[r]->(b)，表示一条从节点a经关联r到节点b的路径。
 备份Neo4j的数据:

1)停掉数据库.
2)备份D:\Neo4J\neo4j-enterprise-1.9.1\data目录下graph.db目录中的所有内容.
3)在服务器上拷贝graph.db目录中的内容到新的服务器的相同目录中,启动即可.

Cypher的基本操作
查询语言包含
START：在图中的开始点，通过元素的ID或所以查找获得。
MATCH：图形的匹配模式，束缚于开始点。
WHERE：过滤条件。
RETURN：返回所需要的。

py2neo支持的语法关键词：
expected whitespace, '.', node labels, '[', "=~", IN, STARTS, ENDS, CONTAINS, IS, '^', '*', '/', '%', '+', '-', '=', "<>", "!=", '<', '>', "<=", ">=", AND, XOR, OR, LOAD CSV, START, MATCH, UNWIND, MERGE, CREATE, SET, DELETE, REMOVE, FOREACH, WITH, CALL, RETURN, UNION, ';' or end 

1）创建节点

            create (a) 创建空节点
            create (a:Person) 创建标签（可以理解为类）为Person的节点
            create (a:Person {name:‘Kaine‘,age:28}) 创建标签为Person，属性name值为Kaine，属性age值为28的节点

创建一个标签的节点：
CREATE (ee:Person { name: "Emil", from: "Sweden", klout: 99 })

创建多个标签（这里是`Person`、`Kaa`）的节点：
CREATE (ee:Person :Kaa { name: "Emil", from: "Sweden", klout: 199 })

使用约束： title被唯一化约束
CREATE CONSTRAINT ON (movie:Movie) ASSERT movie.title IS UNIQUE

删除某个标签（这里是删除`Kaa`）：
MATCH(m:Person) WHERE m.klout = 199 REMOVE m:Kaa RETURN m

重命名某个节点的标签：
MATCH (n:Person) where n.klout = 199 SET  n:NewLabel  REMOVE n:Person RETURN n

# 给所有节点添加一个label:
match (n) set n:AddLabel return count(n)

 2）创建关联

            match (a),(b)
            where a.name=‘Kaine‘ and b.name=‘Sharon‘
            create (a)-[r]->(b) 创建a节点和b节点的路径，此时变量r即代表关联，它也可以有标签

            # 将match和create进行合并使用。将匹配到的节点连接到一个新的节点上。
            match (p:Shuxing {name:"共享wifi最大连接数"}) create (m:Shuxing {name:"5个",alias:"5个",Name:"5个"}) create (p)-[r:next]->(m) return p,r,m;
            match (p:Person {name:"Christopher Guest"}) create (m:School {name:"DEJY",address:"deyang"}) create (p)-[r:ACTED_IN {roles:["studeng"]}]->(m) return p,r,m;

3）查询关键字

        match：用来匹配一定模式，可以是简单的节点、关联，也可以是复杂的路径
        where：用来限定条件，一般是限定match中的出现变量的属性
        return：返回结果
        start：开始节点，一般用于有索引的节点或者关联


        match ... where ... return ...
        如果match有多个对象，用逗号隔开；
        如果where有多个条件，用and连接；
        如果return有多个变量，用逗号隔开


 4）查询举例讲解

        match (n) return n
                       查询所有节点及关联
        match (a)-[r]->(b) where a.name=‘Kaine‘ return a,b
                       查询属性name的值是Kaine的节点，及其所有关联节点
        match (a)-[*1..3]->(b) where a.name=‘Kaine‘ return a,b
                       查询属性name值是Kaine的节点，及其所有距离为1到3的关联节点，
        match (a)-[*2]->(b) where a.name=‘Kaine‘ and not (a)-[*1]->(b) return a,b
                       查询属性name的值是Kaine的节点，及其所有距离为2并且去除距离为1的节点。
                      （在计算好友的好友时会用到，即如果a、b、c三个人都认识，如果仅计算跟a距离为2的人的时候会把b、c也算上，因为a->b->c，或者a->c->b都是通路）

        MATCH (s) WHERE ID(s) = 143 RETURN s  # 根据id查询实体；
        MATCH (v0 : Entity{ name: "系列1"})    RETURN ID(v0)  # 根据实体查询其id
        MATCH p=()-[r:`答案列表`]->() RETURN ID(r)  # 获取`答案列表`这个属性的id；
        
        MATCH (x:Shuxing)-[r]-() RETURN endNode(r)  # 查询所有的叶子节点
        MATCH (a) WHERE ANY (x IN a.alias WHERE x = "价格") RETURN a  # 在列表中查询，若有一个满足及查询成功
        
        MATCH (a) WHERE a.name='Eskil' RETURN a.array, filter(x IN a.array WHERE length(x)= 3)  # 在搜索结果的基础上继续过滤；
       
        match (n) where not (n)-->() return distinct n  # 查找所有的叶子节点

        MATCH (person:Person{name: "张三"}) SET person.ths = CASE person.salary WHEN 'Teacher' THEN 50000 WHEN 'Assistant' THEN 40000 WHEN 85 THEN 'abc' ELSE 'asd' END RETURN person # 选择性赋值

        MATCH p=(n)-[*]->(n1) where n1.name="亚洲"and n.name="支持地区" RETURN length(p)  # 查询两个节点间的路径长度；
 
        注：关联的中括号内数字的含义
            n 距离为n
            ..n 最大距离为n
            n.. 最小距离为n
            m..n 距离在m到n之间

        查询所有节点的labels:
        方法1： call db.labels
        方法2： MATCH (n)
                 WITH DISTINCT labels(n) AS labels
                 UNWIND labels AS label
                 RETURN DISTINCT label
                 ORDER BY label

        # 查询所有节点的关系：
        CALL db.relationshipTypes()

        # 查询所有节点的属性：
        call db.propertyKeys()

        # 查询所有的关系数：
        MATCH ()-->() RETURN count(*);

        # 查询下有什么样的节点，并返回节点的关系及属性等：
        // What kind of nodes exist
        // Sample some nodes, reporting on property and relationship counts per node.
        MATCH (n) WHERE rand() <= 0.1
        RETURN
        DISTINCT labels(n),
        count(*) AS SampleSize,
        avg(size(keys(n))) as Avg_PropertyCount,
        min(size(keys(n))) as Min_PropertyCount,
        max(size(keys(n))) as Max_PropertyCount,
        avg(size( (n)-[]-() ) ) as Avg_RelationshipCount,
        min(size( (n)-[]-() ) ) as Min_RelationshipCount,
        max(size( (n)-[]-() ) ) as Max_RelationshipCount

        # Coalesce:返回表达式中第一个非空值。
        # 有alias属性，则返回alias属性，否则返回name属性；
        match  (n) -[r:定义]-(n2) where n.name= "重大疾病保险金" return coalesce(n2.alias, n2.name)

        # 对关系进行排序，输出第一个非空的关系：
        UNWIND["释义","定义","相关条款"] AS x
        match (n)-[r]->(n2)where n.name='轻症疾病保险金' and type(r)=x
        RETURN type(r), n2.name limit 1

        # 重命名关系：
        match(n1)-[r:`联系人1 `]->(n2) delete r merge (n1)-[r2:`联系人1`]->(n2) return n1, r2, n2

# 属性字符串替换
match(n)where id(n)=1493 set n.name = replace(n.name, '你好', "××") return n.name

以上，若id:1493对应的n.name属性值为int类型的数据就会报错，改成下面这样就好了：
match(n)where n.name =~ '.*你好.*' set n.name = replace(n.name, '你好', "××") return n.name

# 属性值的修改：
方法1：
match(y) SET y.points = y.points + 2

方法2：
如果你有一个像下面这样的参数（把它复制到neo4j浏览器（如：http://192.168.3.145:7474/browser/）来创建一个参数）。
:param someMapping: {a:1, b:2}

下面的查询将在属性a的值为2，且节点类型为SomeLabel的节点上创建属性b，并将该节点上的属性a的值设置为1（属性a的值设置为1，并不是在原有的基础上+1； `+=`意为添加属性b ）。
MATCH (n:SomeLabel) WHERE n.a = 2
SET n+= $someMapping
RETURN n


CALL db.labels() YIELD label
match (n) where labels(n) = label
RETURN n.name, label

CALL db.labels() YIELD label  with DISTINCT label
match (n) where label in labels(n)
RETURN n.name, label

# 查询某些类别的标签
# 方法1：
match(n) where any(l in labels(n) where l in ['Didian', 'Jigou']) return n limit 20

# 方法2：
OPTIONAL MATCH (n1:Label1)
WITH collect(distinct n1) as c1
OPTIONAL MATCH (n2:Label2)
WITH collect(distinct n2) + c1 as c2
OPTIONAL MATCH (n3:Label3)
WITH collect(distinct n3) + c2 as c3
UNWIND c3 as nodes
RETURN count(nodes),labels(nodes)

# 方法3：
MATCH (n:Male)
WHERE n.name =~ '.ail.'
RETURN n
UNION MATCH (n:Female)
WHERE n.name =~ '.ail.'
RETURN n

# 查询只有两个标签的节点：
MATCH (n:Jiaoyinkanglian)with  labels(n) as ln, n where length(ln) =2 return ln, n.name

# 遍历所有属性，并统计包括该属性的节点数：
CALL db.propertyKeys() YIELD propertyKey AS prop
MATCH (n)
WHERE n[prop] IS NOT NULL RETURN prop, count(n) AS numNodes

# 变量查询到的节点：
MATCH p=(n:Abc) RETURN EXTRACT(n in nodes(p) | n.name) as name

# 统计标签对应的节点数：
CALL db.labels() YIELD label
RETURN count(label) AS numLabels

# 统计标签中，包含某个字符串的标签数：
CALL db.labels() YIELD label
WHERE label CONTAINS 'ing'
RETURN count(label) AS numLabels

# 查询所有的关系类型：
call db.relationshipTypes()

# 遍历两个列表搜索：
WITH [1,2,3,4,5,6] as listA, [1,2,3] as listB
RETURN FILTER( n IN listA WHERE NOT n IN listB ) as listC

# 属性值为空时查询示例：
MATCH (n)-[r1:`疾病`]->(n1),(n)-[r2:`赔偿规则`]->(n2),(n)-[r3:`相关条款`]->(n3) WHERE n.name='赔偿情况1'and n1.name = "恶性肿瘤" and r2.等待期 = "是" and r2.意外导致 is null and r2.首次发生 is null RETURN n2.name ,n3.name , r2.name,n.name

# 查询某个节点的关系数：
MATCH (n)-[r]->(n2) where length(()-->(n))=0 and length((n)-->())=1 and length(()-->(n2))=1 and Length((n2)-->())=0  return n, r, n2

# 拆解一个列表：
UNWIND[1,2,3] AS x
RETURN x

WITH [1, 2] AS a,[3, 4] AS b
UNWIND (a + b) AS x
RETURN x

WITH [[1, 2],[3, 4], 5] AS nested
UNWIND nested AS x
UNWIND x AS y
RETURN y

WITH [] AS list
UNWIND
   CASE
      WHEN list = []
         THEN [null]
      ELSE list
   END AS emptylist
RETURN emptylist


# 拆解并去重：
WITH [1,1,2,2] AS coll UNWIND coll AS x
WITH DISTINCT x
RETURN collect(x) AS SET

Collect将所有值收集到一个集合list中。
查询：
START n=node(2,3,4)
RETURN collect(n.property)

a.创建

CREATE (id:label {key:value})
id:     为节点添加一个唯一ID，不设置则系统自动设置一个，不设置时是 CREATE (:label...
label:  标签，生命节点类型
{}:     属性定义，key/value格式

merge在查找时，如果找到则返回，如果没找到则创建。可以避免创建重复的节点
merge (m:School {name:"SCDX"})
on create set m.address="chegndu"
return m;

MATCH (m:School { name:"CDLG" })
MATCH (p:Person { name:"jiaj" })
MERGE (p)-[r:ACTED_IN]->(m)
ON CREATE SET r.roles =['teacher']
RETURN p,r,m ;

MERGE (keanu:Person { name: 'Keanu Reeves' })
ON CREATE SET keanu.created = timestamp()
RETURN keanu.name, keanu.created

MERGE (person:Person)
ON MATCH SET person.found = TRUE RETURN person.name, person.found

MERGE (keanu:Person { name: 'Keanu Reeves' })
ON CREATE SET keanu.created = timestamp()
ON MATCH SET keanu.lastSeen = timestamp()
RETURN keanu.name, keanu.created, keanu.lastSeen

MERGE (person:Person)
ON MATCH SET person.found = TRUE , person.lastAccessed = timestamp()
RETURN person.name, person.found, person.lastAccessed

MATCH (charlie:Person { name: 'Charlie Sheen' }),(wallStreet:Movie { title: 'Wall Street' })
MERGE (charlie)-[r:ACTED_IN]->(wallStreet)
RETURN charlie.name, type(r), wallStreet.title

match (circle:Circle {id:3, cycleId:5})
match (member1:Member {id:1})
match (member2:Member {id:2})
create (invite:Invite {time:timestamp()})-[:TO_CIRCLE]->(circle)
create (invite)-[:INVITED_BY]->(member1)
create (invite)-[:INVITED]->(member2)
return member1, member2, invite, circle;

merge (le:Baoxianmingcheng {name:"安康重疾险"})
create (db:Qingjing{name:"原位癌"})
create
  (le)-[:`责任免除` {name:"指恶性细胞不受控制的进行性增长和扩散。"}]->(db)
RETURN le, db

MATCH (u:user {id : "10662"})-[r:has_profile]->(p:profile), (u2:user)-[r2:has_profile]->(p2:profile)
WHERE
  p.user_id <> p2.user_id AND
  abs(u2.date_birth - u.date_birth) >= 94608000000
RETURN collect(u2.id) as id;

# 多个子句查询：
MATCH (g:Gene),(c:Chromosome)
WHERE g.chromosomeID = c.chromosomeID
CREATE (g)-[:PART_OF]->(c);

＃上子句查询计算量大，可以改成下面这样：
MATCH (c:Chromosome)
WITH c
MATCH (g:Gene)
WHERE g.chromosomeID = c.chromosomeID
CREATE (g)-[:PART_OF]->(c);

# 时间查询处理：
START n = node(0)
MATCH (n)-[:WISH]->(m)
return 1970 + round(m.creationDate / (365.242 * 24 * 3600)), count(m)

MATCH (n)-[:WISH]->(m)
WHERE m.creationDate > '1325376000' AND m.creationDate < '1356998399'
return count(m) as total, '2012' as year

# 创建一个时间节点
CREATE (n:Post {created_at: TIMESTAMP()}) return n

<id>:5346created_at: 1505354974256
时间单位是毫秒，等同于python内：time.time()*1000

＃　安装时间进行排序
MATCH (a:Post) RETURN a ORDER BY a.created_at LIMIT 10

＃ 更多时间处理示例：
MATCH (m25:user)-[:gender]->(male:gender {type:"male"})
where  tofloat(m25.birthday) < (timestamp()-788923800000)
RETURN count(m25)
UNION
MATCH (f25:user)-[:gender]->(female:gender {type:"female"})
where  tofloat(f25.birthday) < (timestamp()-788923800000)
RETURN count(f25)
UNION
...
...or...

MATCH (m25:user)-[:gender]->(male:gender {type:"male"})
where  tofloat(m25.birthday) < (timestamp()-788923800000)
WITH count(m25) AS male25
MATCH (f25:user)-[:gender]->(male:gender {type:"female"})
where  tofloat(f25.birthday) < (timestamp()-788923800000)
WITH count(f25) AS female25, male25

联合查询：
MATCH (a)-[:`包含疾病`]->(b)
where a.name='重大疾病'
RETURN a.name as ZHONGDAJIBING, b.name, "" as QINGZHENGJIBING
UNION
MATCH (a)-[:`包含疾病`]->(b)
where a.name='轻症疾病'
RETURN '' as ZHONGDAJIBING, b.name, a.name as QINGZHENGJIBING


在Neo4j Browser中使用参数
Neo4j Browser是Neo4j内置的浏览器，用于管理数据库，更新数据库和查询数据，再命令窗体中，通过“:”能够引用内置的命令，例如，通过 ":param"能够定义参数，并能够在下一个Cypher语句中引用参数。
1，通过:param命令定义参数
在Neo4j Browser中，输入第一个命令，通过:param 命令定义参数，如下：
:param events:[{year:2014,id:1},{year:2014,id:2}]

2，通过$param引用参数
紧接着，输入Cypher语句，通过$param引用参数，如下：
unwind $events as event
merge (y:Year {year:event.year})
merge(y)<-[:IN]-(e:Event{id:event.id})
RETURN e.id AS x
order by x

b.关系

-[role:label {roles: ["Neo"]}]->
--  表示一个无指向的关系
--> 表示一个有指向的关系
[] 能够添加ID，属性，类型等信息
 另看：http://blog.csdn.net/wangweislk/article/details/47661863



按id查询(这里的id是系统自动创建的)：
start n=node(20) return m;

查询所有节点：
start n=node(*) return n;
查询属性，关系：
start n=node(9) return n,n.name,n.ID,n.level; //查看指定节点，返回需要的属性

start n=node(*) match (n)-[r:SubClassOf]->m return m,n,n.name,n.ID,r; //查找指定关系

模糊查询：
# 根据字符串的前缀进行查询：
MATCH (n:Baoxianjin) where n.name starts WITH '身故' RETURN n LIMIT 25
╒════════════════════════════════════════════════════════╕
│"n"                                                     │
╞════════════════════════════════════════════════════════╡
│{"name":"身故保险金","alias":"身故保险金","Name":"身故保险金"}         │
├────────────────────────────────────────────────────────┤
│{"name":"身故或全残保险金","alias":"身故或全残保险金","Name":"身故或全残保险金"}│
└────────────────────────────────────────────────────────┘

# 根据字符串的后缀进行查询：
MATCH (n:Baoxianjin) where n.name ENDS WITH '保险金' RETURN n LIMIT 25
╒════════════════════════════════════════════════════════╕
│"n"                                                     │
╞════════════════════════════════════════════════════════╡
│{"name":"重大疾病保险金","alias":"重大疾病保险金","Name":"重大疾病保险金"}   │
├────────────────────────────────────────────────────────┤
│{"name":"轻症疾病保险金","alias":"轻症疾病保险金","Name":"轻症疾病保险金"}   │
├────────────────────────────────────────────────────────┤
│{"name":"身故保险金","alias":"身故保险金","Name":"身故保险金"}         │
├────────────────────────────────────────────────────────┤
│{"name":"全残保险金","alias":"全残保险金","Name":"全残保险金"}         │
├────────────────────────────────────────────────────────┤
│{"name":"身故或全残保险金","alias":"身故或全残保险金","Name":"身故或全残保险金"}│
└────────────────────────────────────────────────────────┘

# 根据字符串中间部分进行模糊匹配：
MATCH (n:Baoxianjin) where n.name CONTAINS '故保险' RETURN n LIMIT 25
╒═══════════════════════════════════════════════╕
│"n"                                            │
╞═══════════════════════════════════════════════╡
│{"name":"身故保险金","alias":"身故保险金","Name":"身故保险金"}│
└───────────────────────────────────────────────┘

按关系查询多个节点：
start a = node(14) match b-[r]<->a return r,b;

start a = node(0) match c-[:KNOWS]->b-[:KNOWS]->a return a,b,c; //查找两层KNOWS关系的节点

start a = node(21) match b-[*]->a return a,b;  //查找所有与a节点有关系的节点

根据关系查询：
MATCH p=()-[r:`交费期间`]->() RETURN p LIMIT 25
不能写成下面这样：
MATCH p=()-[`交费期间`]->() RETURN p LIMIT 25
MATCH p=()-[`交费期间`]->() RETURN p LIMIT 25  等同于: MATCH p=()-[r]->() RETURN p LIMIT 25

使用Where条件进行查询：（不用建立Index也可以使用）
start n=node(*) where n.name="Activity" return n;
并且可以使用特定符号：
start n=node(*) where n.ID?="A*" return n;
start n=node(*) where HAS(n.type) return n,n.name,n.ID,n.type; //如果存在属性type，并且以A开头，就输出节点。

配置文件自动建立索引：
修改conf目录下的neo4j.properties文件内容如下，重启Neo4J，对重启后新建的Node生效。
# Enable auto-indexing for nodes, default is false
node_auto_indexing=true

# The node property keys to be auto-indexed, if enabled
node_keys_indexable=name,ID
# Enable auto-indexing for relationships, default is false
relationship_auto_indexing=true

# The relationship property keys to be auto-indexed, if enabled
relationship_keys_indexable=KNOWS,SubClassOf

建立索引后可以用node_auto_index按属性值查询：
start n=node:node_auto_index(name="C") return n,n.name;

修改属性值：
start a = node(*) where a.name="a" set a.name="A" return a,a.name ;
start n=node(0) set n.name="Root",n.ID="001" ; //给默认的根节点添加name,ID属性，便于查询。

对数值型的属性值修改：
MATCH p=(n1)-[r]->(n) where type(r) in ['开始时间','结束时间' ] set n.name=n.name-10000, n.Name=n.Name-10000, n.alias=n.alias-10000  RETURN distinct n

集合：
MATCH p=(n1)-[r]->(n) where type(r) in ['开始时间','结束时间' ] with distinct n.name as name RETURN name

# 去重之后在过滤：
MATCH p=(n1)-[r]->(n) where type(r) in ['开始时间','结束时间' ] with distinct n.name as name, n.Name as Name, n.alias as alias where name>20180102 RETURN name

# 先集合，在查找：
MATCH p=(n1)-[r]->(n) where type(r) in ['开始时间','结束时间' ] with collect(n) as name MATCH (m) where m.name =20171231 RETURN count(m)

MATCH p=(n1)-[r]->(n) where type(r) in ['开始时间','结束时间' ] with collect(n) as name MATCH (m) where m in name RETURN count(m)

MATCH p=(n1)-[r]->(n) where type(r) in ['开始时间','结束时间' ] with collect(n) as name MATCH (m) where m in name set m.name=m.name+10000, m.Name=m.Name+10000, m.alias=m.alias+10000 RETURN m

# 批量更新所有查询结果的数据包括起始节点、中间节点和终止节点都会更新：
MATCH p=(n)-[r:`交费期间`]->(n1) FOREACH (n IN nodes(p) | SET n.available = FALSE )

# 批量删除节点的某个属性：
MATCH p=(n)-[r:`交费期间`]->() FOREACH (n IN nodes(p) | remove n.available  )

# 删除节点的类型（这里删除的是：Intern）
MATCH (soren {name: 'Soren'})
REMOVE soren:Intern
RETURN soren

# 将一个集合展开成一系列数据（UNWIND）：
MATCH p=(n)-[r:`交费期间`]->(n1)
 UNWIND nodes(p) as m
 RETURN n.name, n1.name, m.name
╒═════════╤═════════════════════════════╤═════════════════════════════╕
│"n.name" │"n1.name"                    │"m.name"                     │
╞═════════╪═════════════════════════════╪═════════════════════════════╡
│"A重大疾病保险"│"年交，一次交清/3/5/10/15/20/25/30年"│"A重大疾病保险"                    │
├─────────┼─────────────────────────────┼─────────────────────────────┤
│"A重大疾病保险"│"年交，一次交清/3/5/10/15/20/25/30年"│"年交，一次交清/3/5/10/15/20/25/30年"│
├─────────┼─────────────────────────────┼─────────────────────────────┤
│"B重大疾病保险"│"10年/15年/20年"                │"B重大疾病保险"                    │
├─────────┼─────────────────────────────┼─────────────────────────────┤
│"B重大疾病保险"│"10年/15年/20年"                │"10年/15年/20年"                │
└─────────┴─────────────────────────────┴─────────────────────────────┘


删除：
删除所有节点和关系：
START n=node(*)
match n-[r]-()
delete n,r;

删除所有节点以上方法过时，后面版本将被遗弃-推荐使用如下方法
MATCH (n)
OPTIONAL MATCH (n)-[r]-()
DELETE n,r

插入节点。插入一个Person类别的节点，且这个节点有一个属性name，属性值为Andres
CREATE (n:Person { name : 'Andres'});
插入边。插入一条a到b的有向边，且边的类别为Follow
MATCH (a:Person),(b:Person)
WHERE a.name = 'Node A' AND b.name = 'Node B'
CREATE (a)-[r:Follow]->(b);
更新节点。更新一个Person类别的节点，设置新的name。
MATCH (n:Person { name: 'Andres' })
SET n.name = 'Taylor';
删除节点和与其相连的边。Neo4j中如果一个节点有边相连，是不能单单删除这个节点的。
MATCH (n:Person { name:'Andres' })
DETACH DELETE n;
删除边。
MATCH (a:Person)-[r:Follow]->(b:Person)
WHERE a.name = 'Andres' AND b.name = 'Taylor'
DELETE r;
接着就是查询语句了，作为图形数据库，肯定要来点与众不同的查询对吧。

最短路径。
MATCH (ms:Person { name:'Andres' }),(cs:Person { name:'Taylor' }), p = shortestPath((ms)-[r:Follow]-(cs)) RETURN p;
查询两个节点之间的关系。
MATCH (a:Person { name:'Andres' })-[r]->(b:Person { name:'Taylor' })
RETURN type(r);
查询一个节点的所有Follower。
MATCH (:Person { name:'Taylor' })-[r:Follow]->(Person)
RETURN Person.name;
还有更多的操作可以参考官方用户手册：官方用户手册

2.3 创建一个Neo4j数据库

运行Neo4j之后，在软件窗口可以选择数据库创建位置，之后Neo4j就自动为我们创建好了数据库。

创建唯一性约束，同时具有索引效果，类似关系型数据库中的主键。注意，这只能在数据库中还未插入数据时设置。
CREATE CONSTRAINT ON (a:Person) ASSERT a.name IS UNIQUE;
创建索引。
CREATE INDEX ON :Person(name);
Neo4j的CQL提供“CREATE INDEX”命令创建的节点或关系的属性索引。

创建索引的语法：
CREATE INDEX ON :<label_name> (<property_name>)
注意：
冒号（:)操作符是用来指一个节点或关系标签名称。
上述语法描述了它的<PROPERTY_NAME> <LABEL_NAME>节点或关系创建一个新的索引。
示例-
CREATE INDEX ON :Customer (name)

# 删除索引
Neo4j的CQL提供了“DROP INDEX”命令删除节点的关系或关系的现有索引。
DROP INDEX语法：
DROP INDEX ON :<label_name> (<property_name>)
注意：-
冒号（:)操作符是用来指一个节点或关系标签名称。
上面的语法描述它丢弃创建的现有指数节点或关系的<PROPERTY_NAME> <LABEL_NAME>。
示例-
DROP INDEX ON :Customer (name)

# 创建唯一约束
Neo4j的CQL提供了“创建约束”命令创建的节点或关系的属性的唯一约束。
创建唯一约束语法
CREATE CONSTRAINT ON (<label_name>)
ASSERT <property_name> IS UNIQUE
语法说明
S.No.	语法元素	描述
1。	CREATE CONSTRAINT ON	这是一个Neo4j的CQL关键字，创建约束。
2。	<LABEL_NAME>	这是一个节点或关系的标签名称。
3。	ASSERT	这是一个Neo4j的CQL关键字，断言。
4。	<PROPERTY_NAME>	这是一个节点或关系的属性名。
5。	IS UNIQUE	这是一个Neo4j的CQL关键字通知Neo4j的数据库服务器上创建一个唯一约束。
示例：
CREATE CONSTRAINT ON (cc:CreditCard)
ASSERT cc.number IS UNIQUE

# 删除唯一约束
Neo4j的CQL提供了“DROP约束”命令从一个节点或关系的属性删除现有的唯一约束。
删除唯一约束语法：
DROP CONSTRAINT ON (<label_name>)
ASSERT <property_name> IS UNIQUE
语法说明
S.No.	语法元素	描述
1。	DROP CONSTRAINT ON	这是一个Neo4j的CQL关键字。
2。	<LABEL_NAME>	这是一个节点或关系的标签名称。
3。	ASSERT	这是一个Neo4j的CQL关键字，断言。
4。	<PROPERTY_NAME>	这是一个节点或关系的属性名。
5。	IS UNIQUE	这是一个Neo4j的CQL关键字通知Neo4j的数据库服务器上创建一个唯一约束。
示例：
DROP CONSTRAINT ON (cc:CreditCard)
ASSERT cc.number IS UNIQUE

MATCH (cust:Customer),(cc:CreditCard)
WHERE cust.id = "1001" AND cc.id= "5001"
CREATE (cust)-[r:DO_SHOPPING_WITH{shopdate:"12/12/2014",price:55000}]->(cc)
RETURN r

# 对搜索的结果进行排序：
# 升序排列
MATCH (emp:Employee)
RETURN emp.empid,emp.name,emp.salary,emp.deptno
ORDER BY emp.name

# 降序排列
MATCH (emp:Employee)
RETURN emp.empid,emp.name,emp.salary,emp.deptno
ORDER BY emp.name DESC

# 合并查询结果：
UNION子句，它结合并返回来自两个组结果共同行到单个组结果。 它不会从两个节点返回重复的行。
结果列有限制，并从两个结果集的列名称应该是相同的（Neo4j的“AS”子句用于给一些别名。），列的数据类型应该是相同的。

MATCH (cc:CreditCard)
RETURN cc.id as id,cc.number as number,cc.name as name,
   cc.valid_from as valid_from,cc.valid_to as valid_to
UNION
MATCH (dc:DebitCard)
RETURN dc.id as id,dc.number as number,dc.name as name,
   dc.valid_from as valid_from,dc.valid_to as valid_to

# UNION ALL子句
它结合并返回两个结果集的所有行成一个单一的结果集。 它还返回由两个节点重复行。
结果列有限制，并从两个结果集的列名称应该是相同的（Neo4j的“AS”子句用于给一些别名。），列的数据类型应该是相同的。
MATCH (cc:CreditCard)
RETURN cc.id as id,cc.number as number,cc.name as name,
   cc.valid_from as valid_from,cc.valid_to as valid_to
UNION ALL
MATCH (dc:DebitCard)
RETURN dc.id as id,dc.number as number,dc.name as name,
   dc.valid_from as valid_from,dc.valid_to as valid_to

UNION ALL子句不过滤它们重复行。 如果我们使用UNION子句，则过滤重复行。

# 对查询结果数量进行限制
# 返回开始的两条数据：
MATCH (emp:Employee)
RETURN emp
LIMIT 2

# 返回最后的两条数据：
MATCH (emp:Employee)
RETURN emp
SKIP 2

# 创建之前先搜索
MERGE命令创建命令和MATCH命令的组合。
MERGE = CREATE + MATCH
Neo4j的CQL MERGE图中的给定模式命令搜索，如果存在则返回结果
如果它不在图中存在，则它创建新的节点/关系并返回结果。

Neo4j的CQL MERGE语法
MERGE (<node-name>:<label-name>
{
   <Property1-name>:<Pro<rty1-Value>
   .....
   <Propertyn-name>:<Propertyn-Value>
})
示例：
MERGE (gp2:GoogleProfile2{ Id: 201402,Name:"Nokia"})

# 对空的属性进行搜索：
# Id属性不应该包含空值，它不返回NULL值的行。
MATCH (e:Employee)
WHERE e.id IS NOT NULL
RETURN e.id,e.name,e.sal,e.deptno

# ID值为NULL它只返回NULL值的行。
MATCH (e:Employee)
WHERE e.id IS NULL
RETURN e.id,e.name,e.sal,e.deptno

# 字符串函数：大写
MATCH (e:Employee)
RETURN e.id,UPPER(e.name),e.sal,e.deptno

# 字符串函数：小写
MATCH (e:Employee)
RETURN e.id,LOWER(e.name),e.sal,e.deptno

# 字符串函数：分片（e.name列，这个字符串函数返回仅名称的头两个字母。）
MATCH (e:Employee)
RETURN e.id,SUBSTRING(e.name,0,2),e.sal,e.deptno

# 复制一条关系：
MATCH (n1)-[r:FOLLOWS]->(n2)
WITH collect(n2) as endNodes,n1
FOREACH(i in range(0,length(endNodes)-1) | foreach(a in [endNodes[i]] |
 create (n1)-[:NEW_FOLLOWS]->(a)
))

# 复制一条关系，包括复制关系的属性：
MATCH (a)-[r:FOLLOWS]->(c)
WITH COLLECT(r) AS rels, a, c
FOREACH (rel in rels |
       CREATE (a)-[r:NEW_FOLLOWS]->(c)
       SET r+=rel
)

# 创建新的点，可能会重复创建：
match (n2)-[r2]->(n3) where n2.name='Hugo Weaving'
WITH COLLECT(n2) AS n2s, r2, n3
FOREACH(n2 in n2s|
create (copy:Newnode)
set copy=n2)

match (n:Node {name: 'abc'})
with n as map
create (copy:Node {name: 'def'})
set copy.property1 = map.property1, copy.property2 = map.property2
return copy

call apoc.periodic.iterate('
match (r:Label {`domain key` : 'unit-B2'})-[:isPredecessorOf*0..]->(n:`T-Base`)
return distinct n as map
','
create (copy:`T-Base`)
set copy = map, copy.`domain key` = map.`domain key` + '-copy'
with map, copy
create (copy)-[:isCopyOf]->(map)
',{batchSize:10000,iterateList:true})


查看所有的节点数和边数
MATCH (n) RETURN count(n);
MATCH ()-->() RETURN count(*);

插入节点。插入一个Person类别的节点，且这个节点有一个属性name，属性值为Andres
CREATE (n:Person { name : 'Andres'});
插入边。插入一条a到b的有向边，且边的类别为Follow
MATCH (a:Person),(b:Person)
WHERE a.name = 'Node A' AND b.name = 'Node B'
CREATE (a)-[r:Follow]->(b);
更新节点。更新一个Person类别的节点，设置新的name。
MATCH (n:Person { name: 'Andres' })
SET n.name = 'Taylor';
删除节点和与其相连的边。Neo4j中如果一个节点有边相连，是不能单单删除这个节点的。
MATCH (n:Person { name:'Andres' })
DETACH DELETE n;
删除边。
MATCH (a:Person)-[r:Follow]->(b:Person)
WHERE a.name = 'Andres' AND b.name = 'Taylor'
DELETE r;
接着就是查询语句了，作为图形数据库，肯定要来点与众不同的查询对吧。

最短路径。
MATCH (ms:Person { name:'Andres' }),(cs:Person { name:'Taylor' }), p = shortestPath((ms)-[r:Follow]-(cs)) RETURN p;
查询两个节点之间的关系。
MATCH (a:Person { name:'Andres' })-[r]->(b:Person { name:'Taylor' })
RETURN type(r);
查询一个节点的所有Follower。
MATCH (:Person { name:'Taylor' })-[r:Follow]->(Person)
RETURN Person.name;

常用聚合： count, sum, avg, min, max，count(DISTINCT role)，NULL值自动跳过
MATCH (:Person) RETURN count(*) AS people

排序用法：ORDER BY person.age
分页用法：SKIP {offset} LIMIT {count}
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
RETURN a,count(*) AS appearances
ORDER BY appearances DESC LIMIT 10;

聚集聚合
MATCH (m:Movie)<-[:ACTED_IN]-(a:Person)
RETURN m.title AS movie, collect(a.name) AS cast, count(*) AS actors

编写大型语句
# 要组合具有相同结果结构的两个语句的结果，可以使用UNION [ALL]。
若要查找演员和导演，而不使用同时查找两种关系`()-[:ACTED_IN|:DIRECTED]->()`，则可以使用：
MATCH (actor:Person)-[r:ACTED_IN]->(movie:Movie)
RETURN actor.name AS name, type(r) AS acted_in, movie.title AS title
UNION
MATCH (director:Person)-[r:DIRECTED]->(movie:Movie)
RETURN director.name AS name, type(r) AS acted_in, movie.title AS title

# 在Cypher中，可以将一段语句链接在一起，就像在数据流管道中一样。 每个片段都可以从上一个片段中输出，其结果可以进入下一个片段。
您可以使用WITH子句组合各个部分，并声明哪些数据从一个流到另一个。 WITH非常像RETURN，区别在于它不完成查询，但准备下一个部分的输入。 您可以使用与RETURN子句中相同的表达式，聚合，排序和分页。
唯一的区别是你必须别名所有列，否则它们将不可访问。 您在WITH子句中声明的列只能在后续查询部分中使用。
看下面的例子，我们收集一些有人出现的电影，然后过滤掉只出现在一部电影中的电影。
MATCH (person:Person)-[:ACTED_IN]->(m:Movie)
WITH person, count(*) AS appearances, collect(m.title) AS movies
WHERE appearances > 1
RETURN person.name, appearances, movies

# http://neo4j.com/docs/2.2.0/query-functions-scalar.html
13.1. Predicates
ALL
ANY
NONE
SINGLE
EXISTS

13.2. Scalar functions
LENGTH
LENGTH of pattern expression
TYPE
ID
COALESCE
HEAD
LAST
TIMESTAMP
STARTNODE
ENDNODE
TOINT
TOFLOAT
TOSTRING

13.3. Collection functions
NODES
RELATIONSHIPS
LABELS
KEYS
EXTRACT
FILTER
TAIL
RANGE
REDUCE

13.4. Mathematical functions
ABS
ACOS
ASIN
ATAN
ATAN2
COS
COT
DEGREES
E
EXP
FLOOR
HAVERSIN
LOG
LOG10
PI
RADIANS
RAND
ROUND
SIGN
SIN
SQRT
TAN

13.5. String functions
STR
REPLACE
SUBSTRING
LEFT
RIGHT
LTRIM
RTRIM
TRIM
LOWER
UPPER
SPLIT

